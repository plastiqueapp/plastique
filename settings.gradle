pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        maven {
            url "https://maven.fabric.io/public"
        }
    }

    plugins {
        id "com.github.ben-manes.versions" version "0.27.0"
        id "com.github.plnice.canidropjetifier" version "0.5"
        id "com.github.triplet.play" version "2.6.1"
        id "io.gitlab.arturbosch.detekt" version "1.3.0"

        id "org.jetbrains.kotlin.jvm" version "1.3.61"
        id "org.jetbrains.kotlin.android" version "1.3.61"
        id "org.jetbrains.kotlin.android.extensions" version "1.3.61"
        id "org.jetbrains.kotlin.kapt" version "1.3.61"
    }

    resolutionStrategy.eachPlugin {
        if (requested.id.namespace == "com.android") {
            useModule "com.android.tools.build:gradle:3.6.0-rc01"
        } else if (requested.id.id == "com.google.gms.google-services") {
            useModule "com.google.gms:google-services:4.3.3"
        } else if (requested.id.id == "io.fabric") {
            useModule "io.fabric.tools:gradle:1.31.2"
        }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.1.1"
}

def ci = System.getenv("CI") != null

buildCache {
    local {
        enabled = !ci
    }
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"

        if (ci) {
            publishAlways()
            tag "CI"

            def buildUrl = System.getenv("CIRCLE_BUILD_URL")
            if (buildUrl != null) {
                link "Build", buildUrl
            }

            def pullRequestUrl = System.getenv("CIRCLE_PULL_REQUEST")
            if (pullRequestUrl != null) {
                link "Pull Request", pullRequestUrl
            }
        } else {
            tag "Local"
        }
    }
}

include ":libraries:api"
include ":libraries:cache"
include ":libraries:core"
include ":libraries:core-ui"

include ":features:common"
include ":features:auth"
include ":features:collections"
include ":features:comments"
include ":features:deviations"
include ":features:feed"
include ":features:gallery"
include ":features:main"
include ":features:notifications"
include ":features:profile"
include ":features:settings"
include ":features:statuses"
include ":features:users"
include ":features:watch"

include ":app"
