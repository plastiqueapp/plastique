pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
    }

    plugins {
        id "com.github.ben-manes.versions" version "0.28.0"
        id "com.github.plnice.canidropjetifier" version "0.5"
        id "com.github.triplet.play" version "2.7.3"
        id "io.gitlab.arturbosch.detekt" version "1.7.0"

        id "org.jetbrains.kotlin.jvm" version "1.3.71"
        id "org.jetbrains.kotlin.android" version "1.3.71"
        id "org.jetbrains.kotlin.android.extensions" version "1.3.71"
        id "org.jetbrains.kotlin.kapt" version "1.3.71"
    }

    resolutionStrategy.eachPlugin {
        if (requested.id.namespace == "com.android") {
            useModule "com.android.tools.build:gradle:4.0.0-beta03"
        } else if (requested.id.id == "com.google.gms.google-services") {
            useModule "com.google.gms:google-services:4.3.3"
        } else if (requested.id.id == "com.google.firebase.crashlytics") {
            useModule "com.google.firebase:firebase-crashlytics-gradle:2.0.0-beta03"
        }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.1.1"
}

def ci = System.getenv("CI") != null

buildCache {
    local {
        enabled = !ci
    }
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"

        if (ci) {
            publishAlways()
            tag "CI"

            def buildUrl = System.getenv("CIRCLE_BUILD_URL")
            if (buildUrl != null) {
                link "Build", buildUrl
            }

            def pullRequestUrl = System.getenv("CIRCLE_PULL_REQUEST")
            if (pullRequestUrl != null) {
                link "Pull Request", pullRequestUrl
            }

            def runId = System.getenv("GITHUB_RUN_ID")
            if (runId != null) {
                link "Build", "https://github.com/plastiqueapp/plastique/actions/runs/$runId"
            }
        } else {
            tag "Local"
        }
    }
}

include ":libraries:api"
include ":libraries:cache"
include ":libraries:core"
include ":libraries:core-ui"

include ":features:common"
include ":features:auth"
include ":features:collections"
include ":features:comments"
include ":features:deviations"
include ":features:feed"
include ":features:gallery"
include ":features:main"
include ":features:notifications"
include ":features:profile"
include ":features:settings"
include ":features:statuses"
include ":features:users"
include ":features:watch"

include ":app"
