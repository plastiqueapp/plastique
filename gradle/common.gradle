apply from: "$rootDir/gradle/dependencies.gradle"

plugins.whenPluginAdded { plugin ->
    if (plugin.class.name in ["com.android.build.gradle.AppPlugin",
                              "com.android.build.gradle.LibraryPlugin",
                              "com.android.build.gradle.DynamicFeaturePlugin"]) {
        android {
            compileSdkVersion buildConfig.compileSdk

            defaultConfig {
                minSdkVersion buildConfig.minSdk
                targetSdkVersion buildConfig.targetSdk

                vectorDrawables.useSupportLibrary = true
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            lintOptions {
                warningsAsErrors true
                textReport true
                textOutput "stdout"
                explainIssues false

                disable "GradleDependency", "ObsoleteLintCustomCheck"
                informational "ContentDescription",
                    "Instantiatable" // Has false-positives in 4.0.0-beta01: https://issuetracker.google.com/issues/148172219
            }

            sourceSets {
                main.java.srcDirs += "src/main/kotlin"
                debug.java.srcDirs += "src/debug/kotlin"
                release.java.srcDirs += "src/release/kotlin"
                test.java.srcDirs += "src/test/kotlin"
                androidTest.java.srcDirs += "src/androidTest/kotlin"
            }
        }
    }
}

plugins.withId("org.jetbrains.kotlin.android.extensions") {
    androidExtensions {
        features = ["parcelize"]
        defaultCacheImplementation = "NONE"
    }
}

plugins.withId("org.jetbrains.kotlin.kapt") {
    kapt {
        correctErrorTypes = true
    }
}
