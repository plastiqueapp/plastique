configurations.all { config ->
    exclude group: "com.google.code.findbugs", module: "jsr305"

    resolutionStrategy {
        force "com.google.auto.value:auto-value-annotations:1.6.6"
        eachDependency { details ->
            if (details.requested.group == "com.google.auto.value" && details.requested.name == "auto-value") {
                details.useTarget "com.google.auto.value:auto-value-annotations:1.6.6"
            }
        }
    }
}

dependencies.ext {
    autoFactory = {
        dependencies.add("compileOnly", "com.google.auto.factory:auto-factory:$versions.autoFactory") {
            transitive = false
        }
        dependencies.add("kapt", "com.google.auto.factory:auto-factory:$versions.autoFactory")
    }
    dagger = {
        dependencies.add("implementation", "com.google.dagger:dagger:$versions.dagger")
        dependencies.add("kapt", "com.google.dagger:dagger-compiler:$versions.dagger")

        // Workaround for https://youtrack.jetbrains.com/issue/KT-27404
        if (plugins.hasPlugin("org.jetbrains.kotlin.android")) {
            dependencies.add("kaptAndroidTest", "com.google.dagger:dagger-compiler:$versions.dagger")
        }

        kapt {
            arguments {
                arg("dagger.warnIfInjectionFactoryNotGeneratedUpstream", "enabled")
            }
        }
    }
}
