plugins {
    // Workaround for NoClassDefFoundError in Kotlin Android plugin
    id "com.android.application" apply false

    id "com.github.ben-manes.versions"
    id "com.github.plnice.canidropjetifier"
    id "io.gitlab.arturbosch.detekt"
}

apply from: "gradle/versions.gradle"

ext.ci = System.getenv("CI") != null
ext.buildConfig = [
    compileSdk : 29,
    minSdk     : 21,
    targetSdk  : 28,

    versionCode: 13,
    versionName: "0.2",
    dbVersion  : 1,
]

repositories {
    mavenCentral()
    jcenter()
}

subprojects {
    apply from: "$rootDir/gradle/common.gradle"

    plugins.withId("org.jetbrains.kotlin.android") {
        project.apply from: "$rootDir/gradle/kotlin.gradle"
    }

    plugins.withId("org.jetbrains.kotlin.jvm") {
        project.apply from: "$rootDir/gradle/kotlin.gradle"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

detekt {
    buildUponDefaultConfig = true
    config = files("config/detekt.yml")
    input = files(rootDir)
}

tasks.named("detekt").configure {
    exclude "**/build/**"
}

dependencyUpdates.rejectVersionIf {
    isNonStableVersion(it.candidate.version) && !isNonStableVersion(it.currentVersion)
}

static def isNonStableVersion(version) {
    ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea"].any { qualifier ->
        version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
    }
}

if (ext.ci) {
    apply from: "gradle/ci.gradle"
}
